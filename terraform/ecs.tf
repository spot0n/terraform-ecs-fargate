# ecs.tf

data "template_file" "app" {
  template = file("${path.module}/templates/ecs/app.json.tpl")

  vars = {
    app_image      = var.app_image
    app_port       = var.app_port
    fargate_cpu    = var.fargate_cpu
    fargate_memory = var.fargate_memory
    aws_region     = var.aws_region
    app_name       = var.app_name
  }
}

resource "aws_ecs_task_definition" "app" {
  family                   = var.app_name
  execution_role_arn       = aws_iam_role.ecs_task_execution_role.arn
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = var.fargate_cpu
  memory                   = var.fargate_memory
  container_definitions    = data.template_file.app.rendered
}

resource "aws_ecs_service" "main" {
  name            = var.app_name
  cluster         = var.aws_ecs_cluster.id
  task_definition = aws_ecs_task_definition.app.arn
  desired_count   = var.app_count
  launch_type     = "FARGATE"

  network_configuration {
    security_groups  = [aws_security_group.ecs_tasks.id]
    subnets          = var.ecs_service_subnets
    assign_public_ip = true
  }
  
  dynamic "load_balancer" {
    for_each = var.add_load_balancer == false ? [] : [var.add_load_balancer]
    content {
      target_group_arn = aws_alb_target_group.app.id
      container_name   = var.app_name
      container_port   = var.app_port
    }
  }

  depends_on = [aws_alb_listener.front_end, aws_iam_role_policy_attachment.ecs_task_execution_role]
  
  lifecycle {
    ignore_changes = [desired_count, task_definition]
  }
}

